import subprocess as sp
import os
import glob
import pandas
import numpy
import sys

GREGOR_DIR = config['results']
workdir: GREGOR_DIR
GREGOR_outputDir = os.path.join(GREGOR_DIR, config['output_dir'])
ANNOTATIONS = eval(config['DATA']['ANNOTATIONS'])
TRAITS = eval(config['PARAMETERS']['TRAITS'])  
GREGOR_INPUT_SNPS = config['DATA']['input_snps']
GREGOR_CONFIG_NAME = config['PARAMETERS']['config_name']
GREGOR_output_figure = os.path.join(GREGOR_DIR, 'figures', config['DATA']['output_fig'])


rule all_GREGOR:
    """
    Run GREGOR and assemble dataframe
    """
    input:
        GREGOR_output_figure,

rule makeBedFiles:
    """Add all annotation paths to GREGOR bedfile - all annotations will be tested in one GREGOR run, so we make one file """
    input:
        annotations = ANNOTATIONS,
    output:
        bedfile = os.path.join(GREGOR_DIR, "bedfile.txt"),
    run:
        if len(input.annotations) == 0:
            print("Empty list of annotations")
            sys.exit(1)
        with open(output.bedfile, 'w') as f:
            for filename in input.annotations:
                f.write(f"{filename}\n")
        
rule setupGREGOR:
    """Provide parameters to make GREGOR .conf file - one file per trait """
    input:
        snpfile = GREGOR_INPUT_SNPS,
        bedfile = rules.makeBedFiles.output.bedfile
    output:
        conf = os.path.join(GREGOR_DIR, GREGOR_CONFIG_NAME),
    params:
        script = config['SCRIPTS']['makeConfFile'],
        population = config['PARAMETERS']['POPULATION'],
        gregorR2Threshold = '{gregor_r2}',
        outputdir = GREGOR_outputDir,
        cores = config['PARAMETERS']['cores']
    shell:
        r"""
        python {params.script} --conffile {output.conf} --snpfile {input.snpfile} --bedfile {input.bedfile} \
        --gregorR2Threshold {params.gregorR2Threshold} --cores {params.cores} --outputdir {params.outputdir} \
        --population {params.population};
        """
        
rule runGREGOR:
    """Run GREGOR """
    input:
        conf = rules.setupGREGOR.output.conf
    output:
        ld = os.path.join(GREGOR_outputDir, "index_SNP/index.snp.LD.txt"),
        stats = os.path.join(GREGOR_outputDir, "StatisticSummaryFile.txt")
    params:
        gregor_version_path = config['PARAMETERS']['gregor_version_path']
    shell:
        r"""
        ionice -c2 -n7 {params.gregor_version_path} --conf {input.conf}
        """
    

rule GREGOR_assembleStats:
    """Assemble dataframe for all traits in one file """
    input:
        stats = expand(rules.runGREGOR.output.stats,
                       trait = TRAITS,
                       gregor_r2 = config['PARAMETERS']['gregor_r2'])
    output:
        stats = os.path.join(GREGOR_DIR, config['DATA']['output']),
    params:
        script = config['SCRIPTS']['assembleDF'],
        nameFieldSeparator = config['PARAMETERS']['nameFieldSeparator'],
        jobFieldSeparator = config['PARAMETERS']['jobFieldSeparator'],
        header = config['PARAMETERS']['header']
    shell:
        """
        python {params.script} --filename {input.stats} --outputfilename {output.stats} --nameFieldSeparator {params.nameFieldSeparator} --jobFieldSeparator {params.jobFieldSeparator} --header {params.header}
        """

rule GREGOR_plotEnrichment:
    """Plot enrichment """
    input:
        rules.GREGOR_assembleStats.output.stats
    output:
        gregor = GREGOR_output_figure
    params:
        script = config['SCRIPTS']['plot']
    shell:
        """
        Rscript {params.script} {input} {output.gregor}
        """
                       
                       
onsuccess:
    shell("""mail -s "GREGOR finished" {config[email]} < {log}""")

onerror:
    print("An error occurred")
    shell("""mail -s "an error occurred in GREGOR workflow" {config[email]} < {log}""")
